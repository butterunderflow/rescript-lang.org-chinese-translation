---
title: "模块"
description: "ReScript modules, module signatures and interface files"
canonical: "/docs/manual/latest/module"
---

# Module | 模块

## Basics | 基础知识

**Modules are like mini files**! They can contain type definitions, `let`
bindings, nested modules, etc.

**模块就像迷你文件！** 可以包含类型定义、`let` 绑定、嵌套模块等。

### Creation | 创建模块

To create a module, use the `module` keyword. The module name must start with a
**capital letter**. Whatever you could place in a `.res` file, you may place
inside a module definition's `{}` block.

想要创建一个模块，使用 `module` 关键字。模块的名字必须是**大写字母** 开头。任何你可以放在 `.res` 文件的东西，你都可以放置在模块定义的 `{}` 块中。

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
module School = {
  type profession = Teacher | Director

  let person1 = Teacher
  let getProfession = (person) =>
    switch person {
    | Teacher => "A teacher"
    | Director => "A director"
    }
}
```
```js
function getProfession(person) {
  if (person) {
    return "A director";
  } else {
    return "A teacher";
  }
}

var School = {
  person1: /* Teacher */0,
  getProfession: getProfession
};
```

</CodeTab>

A module's contents (including types!) can be accessed much like a record's,
using the `.` notation. This demonstrates modules' utility for namespacing.

模块中的内容（包括类型！）可以像记录一样用 `.` 符号访问。下面的代码演示了模块的命名空间的作用。

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let anotherPerson: School.profession = School.Teacher
Js.log(School.getProfession(anotherPerson)) /* "A teacher" */
```
```js
var anotherPerson = /* Teacher */0;
console.log("A teacher");
```

</CodeTab>

Nested modules work too.

也可以定义嵌套模块.

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
module MyModule = {
  module NestedModule = {
    let message = "hello"
  }
}

let message = MyModule.NestedModule.message
```
```js
var NestedModule = {
  message: message
};

var MyModule = {
  NestedModule: NestedModule
};

var message = MyModule.NestedModule.message;
```

</CodeTab>

### `open`ing a module | `打开(open)`一个模块

Constantly referring to a value/type in a module can be tedious. Instead, we can "open" a module and refer to its contents without always prepending them with the
module's name. Instead of writing:

经常通过模块来引用值/类型是很繁琐的。相反，我们可以 `open` 一个模块并直接使用它的内容，而不必总是在内容前面加上模块的名称。除了可以这样写以外：

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let p = School.getProfession(School.person1)
```
```js
var p = School.getProfession(School.person1);
```

</CodeTab>

We can write:

还可以这样写:

<CodeTab labels={["ReScript", "JS Output"]}>

```res
open School
let p = getProfession(person1)
```
```js
var p = School.getProfession(School.person1);
```

</CodeTab>

The content of `School` module are made visible (**not** copied into the file, but simply made visible!) in scope. `profession`, `getProfession` and `person1` will thus correctly be found.

`School` 模块的内容在作用域内是可以见（**不是** 复制到文件中，而是简单的可见！）。这样就可以正确的找到 `profession`、`getProfession` 和 `person1`.

**Use `open` this sparingly, it's convenient, but makes it hard to know where some values come from**. You should usually use `open` in a local scope:

**尽量少使用 `open`，它很方便，但是很难知道一些值是哪来的。** 你通常应该在局部作用域中使用 `open`

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let p = {
  open School
  getProfession(person1)
}
/* School's content isn't visible here anymore */
```
```js
var p = School.getProfession(School.person1);
```

</CodeTab>

### Use `open!` to ignore shadow warnings | 使用 `open!` 来忽略覆盖警告

There are situations where `open` will cause a warning due to existing identifiers (bindings, types) being redefined. Use `open!` to explicitly tell the compiler that this is desired behavior.

有些情况下，由于重新定义了现有的标识符（绑定、类型），`open` 将会导致警告。使用 `open!` 明确告诉编译器这是期望中的行为。

```res
let map = (arr, value) => {
  value
}

// opening Js.Array2 would shadow our previously defined `map`
// `open!` will explicitly turn off the automatic warning
open! Js.Array2
let arr = map([1,2,3], (a) => { a + 1})
```

**Note:** Same as with `open`, don't overuse `open!` statements if not necessary. Use (sub)modules to prevent shadowing issues.

**注意：** 与 `open` 一样，如果没有必要，不要过度使用 `open!` 语句。使用（子）模块来防止覆盖问题。

### Destructuring modules | 解构模块

**Since 9.0.2**

**从 9.0.2 开始**

As an alternative to `open`ing a module, you can also destructure a module's functions and values into separate let bindings (similarly on how we'd destructure an object in JavaScript).

作为 `open` 模块的替代方法，你也可以将模块的函数和值解构为单独的 let 绑定（类似于我们在 JavaScript 中如何解构对象）。

<CodeTab labels={["ReScript", "JS Output"]}>

```res
module User = {
  let user1 = "Anna"
  let user2 = "Franz"
}

// Destructure by name
let {user1, user2} = module(User)

// Destructure with different alias
let {user1: anna, user2: franz} = module(User)
```

```js
var user1 = "Anna";

var user2 = "Franz";

var User = {
  user1: user1,
  user2: user2
};
```

</CodeTab>

**Note:** You can't extract types with module destructuring — use a type alias instead (`type user = User.myUserType`).

**注意：** 你不能使用模块解构提取类型 —— 使用类型别名 (type user = User.myUserType)。

### Extending modules | 扩展模块

Using `include` in a module statically "spreads" a module's content into a new one, thus often fulfill the role of "inheritance" or "mixin".

在模块中使用 `include` 可以静态的”扩展“模块中的内容到新的内容中，因此经常扮演“继承(inheritance)”和“混合(mixin)”的角色。

**Note**: this is equivalent to a compiler-level copy paste. **We heavily discourage `include`**. Use it as last resort!

**注意：** 这相当于编译级别的复制粘贴。**我们强烈反对使用 `include`。** 作为最后的手段！

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
module BaseComponent = {
  let defaultGreeting = "Hello"
  let getAudience = (~excited) => excited ? "world!" : "world"
}

module ActualComponent = {
  /* the content is copied over */
  include BaseComponent
  /* overrides BaseComponent.defaultGreeting */
  let defaultGreeting = "Hey"
  let render = () => defaultGreeting ++ " " ++ getAudience(~excited=true)
}
```
```js
function getAudience(excited) {
  if (excited) {
    return "world!";
  } else {
    return "world";
  }
}

var BaseComponent = {
  defaultGreeting: "Hello",
  getAudience: getAudience
};

var defaultGreeting = "Hey";

function render(param) {
  return "Hey world!";
}

var ActualComponent = {
  getAudience: getAudience,
  defaultGreeting: defaultGreeting,
  render: render
};
```

</CodeTab>

**Note**: `open` and `include` are very different! The former brings a module's content into your current scope, so that you don't have to refer to a value by prefixing it with the module's name every time. The latter **copies over** the definition of a module statically, then also do an `open`.

**注意：** `open` 和 `include` 非常不同！前者将模块的内容带入到当中作用域中，这样你就不必每次都用模块名称作为前缀来引用值。后者静态的复制模块的定义，然后执行打开。

### Every `.res` file is a module | 每一个 `.res` 文件都是一个模块

Every ReScript file is itself compiled to a module of the same name as the file name, capitalized. The file `React.res` implicitly forms a module `React`, which can be seen by other source files.

每个 ReScript 文件本身被编译成与文件名相同名称（首字母大写）的模块。文件 `React.res` 隐式地形成了一个 React 模块，其他源文件可以看到这个模块。

**Note**: ReScript file names should, by convention, be capitalized so that their casing matches their module name. Uncapitalized file names are not invalid, but will be implicitly transformed into a capitalized module name. I.e. `file.res` will be compiled into the module `File`. To simplify and minimize the disconnect here, the convention is therefore to capitalize file names.

**注意：** 按照惯例，ReScript 文件名应该大写，以便它们的大小写与模块名称匹配。未大写的文件名不是无效的，而是将隐式地转换为大写的模块名称。例如，`file.res` 将会被编译到 File 模块中。为了简化和减少这种断开的感觉，惯例是将文件名大写。

## Signatures | 签名

A module's type is called a "signature", and can be written explicitly. If a
module is like a `.res` (implementation) file, then a module's signature is like
a `.resi` (interface) file.

模块的类型称为“签名”，可以显式地编写。如果模块类似于 `.res` （实现）文件，则模块的签名类似于 `.resi` （接口）文件。

### Creation | 创建签名

To create a signature, use the `module type` keyword. The signature name must start with a
**capital letter**. Whatever you could place in a `.resi` file, you may place
inside a signature definition's `{}` block.

想要创建签名，使用 `module type` 关键字。签名必须以**大写字母** 开头。任何你可以放在 `.resi` 文件的东西，你都可以放置在签名定义的 `{}` 块中。

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
/* Picking up previous section's example */
module type EstablishmentType = {
  type profession
  let getProfession: profession => string
}
```
```js
// Empty output
```

</CodeTab>

A signature defines the list of requirements that a module must satisfy in order
for that module to match the signature. Those requirements are of the form:

签名定义了模块为了与签名匹配而必须满足的需求列表。这些要求的形式如下：

- `let x: int` requires a `let` binding named `x`, of type `int`.
- `type t = someType` requires a type field `t` to be equal to `someType`.
- `type t` requires a type field `t`, but without imposing any requirements on the actual, concrete type of `t`. We'd use `t` in other entries in the signature to describe relationships, e.g. `let makePair: t => (t, t)` but we cannot, for example, assume that `t` is an `int`. This gives us great, enforced abstraction abilities.

- `let x: int` 需要一个名为 `x` 的 `let` 绑定，类型为 `int`。
- `type t = someType` 要求类型字段 `t` 等于 `someType`。
- `type t` 需要一个类型字段 `t`，但没实际的、具体的类型要求。我们会在签名的其他条目中使用 `t` 来描述关系。例如，`let makePair: t => (t, t)` 但是我们不能假设 `t` 是一个 int。这给了我们强大的抽象能力。

To illustrate the various kinds of type entries, consider the above signature
`EstablishmentType` which requires that a module:

为了说明这种类型的条目，请考虑上述签名 `EstablishmentType`，它要求一个模块：

- Declare a type named `profession`.
- Must include a function that takes in a value of the type `profession` and returns a string.

- 声明了一个名为 `profession` 的类型。
- 必须包含一个函数，该函数接受一个 `profession` 类型的值并返回字符串。

**Note**:

**注意：**

Modules of the type `EstablishmentType` can contain more fields than the
signature declares, just like the module `School` in the previous section (if we
choose to assign it the type `EstablishmentType`. Otherwise, `School` exposes
every field). This effectively makes the `person1` field an enforced
implementation detail! Outsiders can't access it, since it's not present in the
signature; the signature **constrained** what others can access.

`EstablishmentType` 类型的模块可以包含比此签名更多的字段，就像上一节的模块 `School`（如果我们选择为其分配 `EstablishmentType` 类型。否则，`School` 会暴露所有字段）。这有效地使得 `person1` 字段称为一个强制实现的细节！外部无法访问它，因为它不存在于签名中；签名**限制了** 其他人可以访问的东西。

The type `EstablishmentType.profession` is **abstract**: it doesn't have a
concrete type; it's saying "I don't care what the actual type is, but it's used
as input to `getProfession`". This is useful to fit many modules under the same
interface:

类型 `EstablishmentType.profession` 是**抽象的：** 它表示“我不关心实际的类型是什么，但是它被用作 `getProfession` 的输入”。这对于在同一接口下实现多个模块很有用：
接口：

<CodeTab labels={["ReScript", "JS Output"]}>

```res
module Company: EstablishmentType = {
  type profession = CEO | Designer | Engineer | ...

  let getProfession = (person) => ...
  let person1 = ...
  let person2 = ...
}
```
```js
function getProfession(person) {
  ...
}

var person1 = ...

var person2 = ...

var Company = {
  getProfession: getProfession,
  person1: person1,
  person2: person2
};
```

</CodeTab>

It's also useful to hide the underlying type as an implementation detail others
can't rely on. If you ask what the type of `Company.profession` is, instead of
exposing the variant, it'll only tell you "it's `Company.profession`".

隐藏底层类型作为其他人无法依赖的实现细节也很有用。如果你询问 `Company.profession` 的类型是什么，它不会公开变体，而只会告诉你它是 `Company.profession`。

### Extending module signatures | 扩展模块签名

Like modules themselves, module signatures can also be extended by other module signatures using `include`. Again, **heavily discouraged**:

像模块本身一样，模块签名也可以通过使用 `include` 的其他模块签名进行扩展。再次, **我们非常不鼓励使用**:：

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
module type BaseComponent = {
  let defaultGreeting: string
  let getAudience: (~excited: bool) => string
}

module type ActualComponent = {
  /* the BaseComponent signature is copied over */
  include BaseComponent
  let render: unit => string
}
```
```js
// Empty output
```

</CodeTab>

**Note**: `BaseComponent` is a module **type**, not an actual module itself!

**注意：** `BaseComponent` 是一个模块 **类型** , 不是实际的模块本身！

If you do not have a defined module type, you can extract it from an actual module
using `include (module type of ActualModuleName)`. For example, we can extend the
`List` module from the standard library, which does not define a module
type.

如果没有已定义的模块类型，则可以使用 `include (module type of ActualModuleName)` 提取该模块的类型。例如，我们可以从标准库中扩展 `List` 模块，该模块没有定义模块类型。

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
module type MyList = {
  include (module type of List)
  let myListFun: list<'a> => list<'a>
}
```
```js
// Empty output
```

</CodeTab>

### Every `.resi` file is a signature | 每个 `.resi` 文件都是一个签名

Similar to how a `React.res` file implicitly defines a module `React`, a file
`React.resi` implicitly defines a signature for `React`. If `React.resi` isn't
provided, the signature of `React.res` defaults to exposing all the fields of the
module. Because they don't contain implementation files, `.resi` files are used
in the ecosystem to also document the public API of their corresponding modules.

与 `React.res` 文件隐式地定义 `React` 模块类似，`React.resi` 隐式地定义 `React` 签名。如果没有提供 `React.resi`， `React.res` 的签名默认显示模块的所有字段。因为它们不包含实现文件。在生态系统中，`.resi` 文件还用于记录相应模块的公共 API。

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
/* file React.res (implementation. Compiles to module React) */
type state = int
let render = (str) => str
```
```js
function render(str) {
  return str;
}
```

</CodeTab>

```res sig
/* file React.resi (interface. Compiles to the signature of React.res) */
type state = int
let render: string => string
```

## Module Functions (functors) | 模块函数（仿函数）

Modules can be passed to functions! It would be the equivalent of passing a file
as a first-class item. However, modules are at a different "layer" of the
language than other common concepts, so we can't pass them to *regular*
functions. Instead, we pass them to special functions called "functors".

模块可以传递函数！它相当于将文件作为第一类子项传递。但是，模块与其他常见概念在语言的“层”上是不同的，因此我们不能将它们传递给常规函数。相反，我们将它们传递给称为“仿函数”的特殊函数。

The syntax for defining and using functors is very much like the syntax
for defining and using regular functions. The primary differences are:

用于定义和使用仿函数的语法和使用常规函数非常类似，主要区别是：

- Functors use the `module` keyword instead of `let`.
- Functors take modules as arguments and return a module.
- Functors *require* annotating arguments.
- Functors must start with a capital letter (just like modules/signatures).

- 仿函数使用 `module` 关键字替代 `let`。
- 仿函数将模块作为参数并返回一个模块。
- 仿函数*需要*注释参数。
- 仿函数必须以大写字母开头（就像模块/签名一样）。

Here's an example `MakeSet` functor, that takes in a module of the type
`Comparable` and returns a new set that can contain such comparable items.

下面是一个 `MakeSet` 仿函数实例，它接受类型为 `Comparable` 的模块，并返回一个新的集合，该集合包含 comparable 子项。

<CodeTab labels={["ReScript", "JS Output"]}>

```res prelude
module type Comparable = {
  type t
  let equal: (t, t) => bool
}

module MakeSet = (Item: Comparable) => {
  // let's use a list as our naive backing data structure
  type backingType = list<Item.t>
  let empty = list{}
  let add = (currentSet: backingType, newItem: Item.t): backingType =>
    // if item exists
    if List.exists(x => Item.equal(x, newItem), currentSet) {
      currentSet // return the same (immutable) set (a list really)
    } else {
      list{
        newItem,
        ...currentSet // prepend to the set and return it
      }
    }
}
```
```js
var List = require("./stdlib/list.js");

function MakeSet(Item) {
  var add = function(currentSet, newItem) {
    if (
      List.exists(function(x) {
        return Item.equal(x, newItem);
      }, currentSet)
    ) {
      return currentSet;
    } else {
      return {
        hd: newItem,
        tl: currentSet,
      };
    }
  };
  return {
    empty: /* [] */ 0,
    add: add,
  };
}
```

</CodeTab>

Functors can be applied using function application syntax. In this case, we're
creating a set, whose items are pairs of integers.

仿函数可以使用函数应用程序语法。在本例中，我们创建了一个集合，他的子项为整数对。

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
module IntPair = {
  type t = (int, int)
  let equal = ((x1: int, y1: int), (x2, y2)) => x1 == x2 && y1 == y2
  let create = (x, y) => (x, y)
}

/* IntPair abides by the Comparable signature required by MakeSet */
module SetOfIntPairs = MakeSet(IntPair)
```
```js
function equal(param, param$1) {
  if (param[0] === param$1[0]) {
    return param[1] === param$1[1];
  } else {
    return false;
  }
}

function create(x, y) {
  return [x, y];
}

var IntPair = {
  equal: equal,
  create: create,
};

var SetOfIntPairs = {
  empty: /* [] */ 0,
  add: add,
};
```

</CodeTab>

### Module functions types | 模块函数类型

Like with module types, functor types also act to constrain and hide what we may
assume about functors. The syntax for functor types are consistent with those
for function types, but with types capitalized to represent the signatures of
modules the functor accepts as arguments and return values. In the
previous example, we're exposing the backing type of a set; by giving `MakeSet`
a functor signature, we can hide the underlying data structure!

与模块类型一样，仿函数类型也起到约束和隐藏仿函数的作用。仿函数类型的语法与函数类型的语法一致，但是大写的类型表示模块的签名，仿函数接受参数，并且返回一个值。在签名的实例中，我们公开了一个集合的支持类型，通过给 `MakeSet` 一个函数签名，我们可以隐藏底层的数据结构！

<CodeTab labels={["ReScript", "JS Output"]}>

```res
module type Comparable = ...

module type MakeSetType = (Item: Comparable) => {
  type backingType
  let empty: backingType
  let add: (backingType, Item.t) => backingType
}

module MakeSet: MakeSetType = (Item: Comparable) => {
  ...
}
```
```js
// Empty output
```

</CodeTab>

## Exotic Module Filenames | 异常模块文件名

**Since 8.3**

**从 8.3 开始**

It is possible to use non-conventional characters in your filenames (which is sometimes needed for specific JS frameworks). Here are some examples:

可以在文件名中使用非常规字符（这有时是特定 JS 框架所需要的）。下面是一些例子：

- `src/Button.ios.res`
- `pages/[id].res`

Please note that modules with an exotic filename will not be accessible from other ReScript modules.

请注意，其他 ReScript 模块不能访问具有异常文件名的模块。

## Tips & Tricks | 提示与技巧

Modules and functors are at a different "layer" of language than the rest (functions, let bindings, data structures, etc.). For example, you can't easily pass them into a tuple or record. Use them judiciously, if ever! Lots of times, just a record or a function is enough.

模块和仿函数与语言的其他部分（函数、let 绑定、数据结构等）处于不同的“层”。例如，你不能轻易的将它们传递到元祖或记录中。如果需要的话，要谨慎的使用它们！多次使用，只需要一个记录或一个函数就够了。

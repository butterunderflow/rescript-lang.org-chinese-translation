---
title: "函数"
description: "Function syntax in ReScript"
canonical: "/docs/manual/latest/function"
---

# 函数

_Cheat sheet for the full function syntax at the end_.

_所有关于函数的语法清单在本章节的最后面_

ReScript functions are declared with an arrow and return an expression, just like JS functions. They compile to clean JS functions too.

ReScript函数用箭头声明, 并会返回一个表达式, 就像JS中的函数一样. 它们也会编译成简洁的的JS函数.

<CodeTab labels={["ReScript", "JS Output"]}>

```res prelude
let greet = (name) => "Hello " ++ name
```
```js
function greet(name) {
  return "Hello " + name;
}
```

</CodeTab>

This declares a function and assigns to it the name `greet`, which you can call like so:

这会声明一个函数, 并给它指定了`greet`的名字, 你可以这样调用它.

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
greet("world!") // "Hello world!"
```
```js
greet("world!");
```

</CodeTab>

Multi-arguments functions have arguments separated by comma:

多参数函数的参数由逗号分隔:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let add = (x, y, z) => x + y + z
add(1, 2, 3) // 6
```
```js
function add(x, y, z) {
  return (x + y | 0) + z | 0;
}
```

</CodeTab>

For longer functions, you'd surround the body with a block:

对于较长的函数, 你可以把函数体用大括号包裹起来:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let greetMore = (name) => {
  let part1 = "Hello"
  part1 ++ " " ++ name
}
```
```js
function greetMore(name) {
  return "Hello " + name;
}
```

</CodeTab>

If your function has no argument, just write `let greetMore = () => {...}`.

如果你的函数没有参数, 只需要写 `let greetMore = () => {...}`.

## Labeled Arguments | 标签参数

Multi-arguments functions, especially those whose arguments are of the same type, can be confusing to call.

多参数函数, 特别是那些参数类型相同的函数, 在调用时可能会很混乱.

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let addCoordinates = (x, y) => {
  // use x and y here
}
// ...
addCoordinates(5, 6) // which is x, which is y?
```
```js
function addCoordinates(x, y) {
  // use x and y here
}

addCoordinates(5, 6);
```

</CodeTab>

You can attach labels to an argument by prefixing the name with the `~` symbol:

你可以在一个参数上附加标签, 方式是在参数名字前加上`~`符号.

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let addCoordinates = (~x, ~y) => {
  // use x and y here
}
// ...
addCoordinates(~x=5, ~y=6)
```
```js
function addCoordinates(x, y) {
  // use x and y here
}

addCoordinates(5, 6);
```

</CodeTab>

You can provide the arguments in **any order**:

你可以按**任意顺序**传入标签参数

<CodeTab labels={["ReScript", "JS Output"]}>

```res
addCoordinates(~y=6, ~x=5)
```
```js
addCoordinates(5, 6);
```

</CodeTab>

The `~x` part in the declaration means the function accepts an argument labeled `x` and can refer to it in the function body by the same name. You can also refer to the arguments inside the function body by a different name for conciseness:

声明中的`~x`部分意味着该函数接受一个标签为`x`的参数, 并且可以在函数体中用相同的名称来指代它. 你也可以在函数体中用不同的名字来指代参数以提升简洁性:

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let drawCircle = (~radius as r, ~color as c) => {
  setColor(c)
  startAt(r, r)
  // ...
}

drawCircle(~radius=10, ~color="red")
```
```js
function drawCircle(r, c) {
  setColor(c);
  return startAt(r, r);
}

drawCircle(10, "red");
```

</CodeTab>

As a matter of fact, `(~radius)` is just a shorthand for `(~radius as radius)`.

事实上, `(~radius)`只是`(~radius as radius)`的一个缩写.

Here's the syntax for typing the arguments:

下面是给参数加上类型的语法:

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let drawCircle = (~radius as r: int, ~color as c: string) => {
  // code here
}
```
```js
function drawCircle(r, c) {
  // code here
}
```

</CodeTab>

## Optional Labeled Arguments | 可选的标签参数

Labeled function arguments can be made optional during declaration. You can then omit them when calling the function.

标记的函数参数可以在声明为可选的, 然后就可以在调用函数时省略它们了:

<CodeTab labels={["ReScript", "JS Output"]}>

```res
// radius can be omitted
let drawCircle = (~color, ~radius=?, ()) => {
  setColor(color)
  switch radius {
  | None => startAt(1, 1)
  | Some(r_) => startAt(r_, r_)
  }
}
```
```js
var Caml_option = require("./stdlib/caml_option.js");

function drawCircle(color, radius, param) {
  setColor(color);
  if (radius === undefined) {
    return startAt(1, 1);
  }
  var r_ = Caml_option.valFromOption(radius);
  return startAt(r_, r_);
}
```

</CodeTab>

When given in this syntax, `radius` is **wrapped** in the standard library's `option` type, defaulting to `None`. If provided, it'll be wrapped with a `Some`. So `radius`'s type value is `None | Some(int)` here.

当使用这种语法的时候, radius是被**包裹**在标准库的`option`类型中的, 默认值为`None`. 如果提供了, 那么会被包裹到构造器`Some`中. 所以, 这里`radius`的类型可能可能值有`None| Some(int)`.

More on `option` type [here](null-undefined-option.md).

在[这里](null-undefined-option.md)查看关于`option`类型的更多信息.

**Note** for the sake of the type system, whenever you have an optional argument, you need to ensure that there's also at least one positional argument (aka non-labeled, non-optional argument) after it. If there's none, provide a dummy `unit` (aka `()`) argument.

**注意**为了类型系统的缘故, 只要你有一个可选参数, 你就需要确保在它后面至少有一个位置参数(positional argument, 又称非标记的, 非可选的参数). 如果没有, 可以提供一个假的 `unit`(也就是`()`)参数.

### Signatures and Type Annotations | 签名和类型注解

Functions with optional labeled arguments can be confusing when it comes to signature and type annotations. Indeed, the type of an optional labeled argument looks different depending on whether you're calling the function, or working inside the function body. Outside the function, a raw value is either passed in (`int`, for example), or left off entirely. Inside the function, the parameter is always there, but its value is an option (`option<int>`). This means that the type signature is different, depending on whether you're writing out the function type, or the parameter type annotation. The first being a raw value, and the second being an option.

当涉及到签名和类型注释时, 带有可选标记参数的函数可能会令人困惑. 事实上, 一个可选的标记参数的类型看起来是不同的, 这取决于你是在函数的调用点, 还是在函数体内部. 在函数外部, 一个原始值要么被传入(例如, `int`), 要么被完全舍弃不传入值. 在函数内部, 参数总是存在的, 但其值是一个选项(`option<int>`). 这意味着类型签名是不同的, 取决于你是写出函数类型, 还是为参数写类型注释, 前者是一个原始值, 后者是一个`option`.

If we get back to our previous example and both add a signature and type annotations to its argument, we get this:

如果我们回到之前的例子, 同时给它添加一个签名和参数类型注释, 我们会得到这样的结果.

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let drawCircle: (~color: color, ~radius: int=?, unit) => unit =
  (~color: color, ~radius: option<int>=?, ()) => {
    setColor(color)
    switch radius {
    | None => startAt(1, 1)
    | Some(r_) => startAt(r_, r_)
    }
  }
```
```js
function drawCircle(color, radius, param) {
  setColor(color);
  if (radius !== undefined) {
    return startAt(radius, radius);
  } else {
    return startAt(1, 1);
  }
}
```

</CodeTab>

The first line is the function's signature, we would define it like that in an interface file (see [Signatures](module.md#signatures)). The function's signature describes the types that the **outside world** interacts with, hence the type `int` for `radius` because it indeed expects an `int` when called.

第一行是函数的签名, 我们会在接口文件中这样定义它(看[签名](module.md#signatures)章节). 该函数的签名描述了**外部世界**交互的类型, 因此 `radius` 的类型是 `int`, 因为它在调用时期望的输入是 `int`.

In the second line, we annotate the arguments to help us remember the types of the arguments when we use them **inside** the function's body, here indeed `radius` will be an `option<int>` inside the function.

在第二行中, 我们对参数进行注解, 以帮助我们在**函数内部**使用参数时记住参数的类型, 这里`radius`在函数内部将是一个`option<int>`.

So if you happen to struggle when writing the signature of a function with optional labeled arguments, try to remember this!

因此如果您在编写带有可选标记参数的函数签名时碰巧遇到了困难, 请记住这一点!

### Explicitly Passed Optional | 显式的传入可选值

Sometimes, you might want to forward a value to a function without knowing whether the value is `None` or `Some(a)`. Naively, you'd do:

有时, 您可能希望将值转发给函数, 但不确定该值是`None`还是`Some(a)`. 天真地, 你会这样做:

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let result =
  switch payloadRadius {
  | None => drawCircle(~color, ())
  | Some(r) => drawCircle(~color, ~radius=r, ())
  }
```
```js
var r = payloadRadius;

var result = r !== undefined
  ? drawCircle(color, Caml_option.valFromOption(r), undefined)
  : drawCircle(color, undefined);
```

</CodeTab>

This quickly gets tedious. We provide a shortcut:

这很快就会让代码变得冗长. 我们提供了一个简短写法:

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let result = drawCircle(~color, ~radius=?payloadRadius, ())
```
```js
var result = drawCircle(1, undefined, undefined);
```

</CodeTab>

This means "I understand `radius` is optional, and that when I pass it a value it needs to be an `int`, but I don't know whether the value I'm passing is `None` or `Some(val)`, so I'll pass you the whole `option` wrapper".

这意味着 "我知道`radius`是可选值, 当我给它传递一个值时, 它需要是一个`int`, 但我不确定我传递的值是`None`还是`Some(val)`, 所以我给你传递整个`option`的包装".

### Optional with Default Value | 默认参数的可选值

Optional labeled arguments can also be provided a default value. In this case, they aren't wrapped in an `option` type.

可选的标签参数也可以提供一个默认值, 这种情况下, 它们不能被包裹在`option`类型中.

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let drawCircle = (~radius=1, ~color, ()) => {
  setColor(color)
  startAt(radius, radius)
}
```
```js
function drawCircle(radiusOpt, color, param) {
  var radius = radiusOpt !== undefined ? radiusOpt : 1;
  setColor(color);
  return startAt(radius, radius);
}
```

</CodeTab>

## Recursive Functions | 递归函数

ReScript chooses the sane default of preventing a function to be called recursively within itself. To make a function recursive, add the `rec` keyword after the `let`:

ReScript选择了将函数不能在内部调用自身设置为默认行为. 要创建一个递归函数, 在`let`后面加上`rec`关键字:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let rec neverTerminate = () => neverTerminate()
```
```js
function neverTerminate(_param) {
  while(true) {
    _param = undefined;
    continue ;
  };
}
```

</CodeTab>

A simple recursive function may look like this:

一个简单的递归函数可以这样定义:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
// Recursively check every item on the list until one equals the `item`
// argument. If a match is found, return `true`, otherwise return `false`
let rec listHas = (list, item) =>
  switch list {
  | list{} => false
  | list{a, ...rest} => a === item || listHas(rest, item)
  }
```
```js
function listHas(_list, item) {
  while(true) {
    var list = _list;
    if (!list) {
      return false;
    }
    if (list.hd === item) {
      return true;
    }
    _list = list.tl;
    continue ;
  };
}
```

</CodeTab>

Recursively calling a function is bad for performance and the call stack. However, ReScript intelligently compiles [tail recursion](https://stackoverflow.com/questions/33923/what-is-tail-recursion) into a fast JavaScript loop. Try checking the JS output of the above code!

### Mutually Recursive Functions | 互递归函数

Mutually recursive functions start like a single recursive function using the
`rec` keyword, and then are chained together with `and`:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let rec callSecond = () => callFirst()
and callFirst = () => callSecond()
```
```js
function callSecond(_param) {
  while(true) {
    _param = undefined;
    continue ;
  };
}

function callFirst(_param) {
  while(true) {
    _param = undefined;
    continue ;
  };
}
```

</CodeTab>

## Uncurried Function | 去柯里化的函数

ReScript's functions are curried by default, which is one of the few performance penalties we pay in the compiled JS output. The compiler does a best-effort job at removing those currying whenever possible. However, in certain edge cases, you might want guaranteed uncurrying. In those cases, put a dot in the function's parameter list:

ReScript的函数是默认柯里化的, 这是少数的编译到JS会产生的性能开销情况之一. 编译器会尽最大可能地去除这些柯里化, 然而, 有一些边界情况, 你可能想要保证柯里化不会发生. 这些情况, 可以放一个点`.`在函数的参数列表前:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let add = (. x, y) => x + y

add(. 1, 2)
```
```js
function add(x, y) {
  return x + y | 0;
}

add(1, 2);
```

</CodeTab>

If you write down the uncurried function's type, you'll add a dot there as well.

如果你写下非柯里化函数的参数, 你也要在那加一个点`.` . 

**Note**: both the declaration site and the call site need to have the uncurry annotation. That's part of the guarantee/requirement.

**注意**: 声明的位置和调用点都要加上禁止柯里化的标记, 这样是保证函数不会被柯里化的条件.

**This feature seems trivial**, but is actually one of our most important features, as a primarily functional language. We encourage you to use it if you'd like to remove any mention of `Curry` runtime in the JS output.

**这个特性看起来是trivial的**, 但是这其实是我们作为一门函数式语言最重要的特性之一. 如果你想要在JS的编译结果中移除所有运行时的`柯里化`, 我们鼓励你使用它.

## The ignore() Function | ignore() 函数

Occasionally you may want to ignore the return value of a function. ReScript provides an `ignore()` function that discards the value of its argument and returns `()`:

偶尔你可能想忽略一个函数的返回值, ReScript提供了一个`ignore()'函数, 它抛弃了其参数的值, 并返回`()`.

<CodeTab labels={["ReScript", "JS Output"]}>

```res
mySideEffect()->Promise.catch(handleError)->ignore

Js.Global.setTimeout(myFunc, 1000)->ignore
```

```js
$$Promise.$$catch(mySideEffect(), function (prim) {
  return handleError(prim);
});

setTimeout(function (prim) {
  myFunc();
}, 1000);
```

</CodeTab>

## Tips & Tricks | 技巧 & 诀窍

Cheat sheet for the function syntaxes:

有关函数的语法清单:

### Declaration | 函数声明

```res
// anonymous function
(x, y) => 1
// bind to a name
let add = (x, y) => 1

// labeled
let add = (~first as x, ~second as y) => x + y
// with punning sugar
let add = (~first, ~second) => first + second

// labeled with default value
let add = (~first as x=1, ~second as y=2) => x + y
// with punning
let add = (~first=1, ~second=2) => first + second

// optional
let add = (~first as x=?, ~second as y=?) => switch x {...}
// with punning
let add = (~first=?, ~second=?) => switch first {...}
```

#### With Type Annotation | 带有类型的函数声明

```res
// anonymous function
(x: int, y: int): int => 1
// bind to a name
let add = (x: int, y: int): int => 1

// labeled
let add = (~first as x: int, ~second as y: int) : int => x + y
// with punning sugar
let add = (~first: int, ~second: int) : int => first + second

// labeled with default value
let add = (~first as x: int=1, ~second as y: int=2) : int => x + y
// with punning sugar
let add = (~first: int=1, ~second: int=2) : int => first + second

// optional
let add = (~first as x: option<int>=?, ~second as y: option<int>=?) : int => switch x {...}
// with punning sugar
// note that the caller would pass an `int`, not `option<int>`
// Inside the function, `first` and `second` are `option<int>`.
let add = (~first: option<int>=?, ~second: option<int>=?) : int => switch first {...}
```

### Application | 函数应用

```res
add(x, y)

// labeled
add(~first=1, ~second=2)
// with punning sugar
add(~first, ~second)

// application with default value. Same as normal application
add(~first=1, ~second=2)

// explicit optional application
add(~first=?Some(1), ~second=?Some(2))
// with punning
add(~first?, ~second?)
```

#### With Type Annotation | 在函数应用中加上类型注解

```res
// labeled
add(~first=1: int, ~second=2: int)
// with punning sugar
add(~first: int, ~second: int)

// application with default value. Same as normal application
add(~first=1: int, ~second=2: int)

// explicit optional application
add(~first=?Some(1): option<int>, ~second=?Some(2): option<int>)
// no punning sugar when you want to type annotate
```

### Standalone Type Signature | 分离的类型签名

```res
// first arg type, second arg type, return type
type add = (int, int) => int

// labeled
type add = (~first: int, ~second: int) => int

// labeled
type add = (~first: int=?, ~second: int=?, unit) => int
```

#### In Interface Files | 在接口文件中加上类型签名

To annotate a function from the implementation file (`.res`) in your interface file (`.resi`):

要在你的接口文件(`.res`)中为一个来自实现文件(`.res`)的函数加上类型注解:

```res sig
let add: (int, int) => int
```

The type annotation part is the same as the previous section on With Type Annotation.

类型签名的部分和之前是一样的.

**Don't** confuse `let add: myType` with `type add = myType`. When used in `.resi` interface files, the former exports the binding `add` while annotating it as type `myType`. The latter exports the type `add`, whose value is the type `myType`.

**不要搞混**`let add: myType` 和 `type add = myType`. 当在`.resi`接口文件中, 前者会导出绑定`add`, 并将其类型标注为`myType`类型; 后者会导出类型`add`, 并声明该类型和`myType`相同. 

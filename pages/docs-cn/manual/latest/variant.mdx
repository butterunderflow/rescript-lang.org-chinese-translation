---
title: "变体"
description: "Variant data structures in ReScript"
canonical: "/docs/manual/latest/variant"
---

# Variant | 变体

So far, most of ReScript's data structures might look familiar to you. This section introduces an extremely important, and perhaps unfamiliar, data structure: variant.

到目前为止, ReScript的大多数数据结构对你来说可能已经很熟悉了.  本节介绍了一个极其重要的, 也许是你之前不熟悉的数据结构: 变体.

Most data structures in most languages are about "this **and** that". A variant allows us to express "this **or** that".

大部分的编程语言中的数据结构都是以"这样**且**那样". 变体的存在让我们表达"这样**或**那样".

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type myResponse =
  | Yes
  | No
  | PrettyMuch

let areYouCrushingIt = Yes
```
```js
var areYouCrushingIt = /* Yes */0;
```

</CodeTab>

`myResponse` is a variant type with the cases `Yes`, `No` and `PrettyMuch`, which are called "variant constructors" (or "variant tag"). The `|` bar separates each constructor.

`myResponse`是一个变体类型, 有`Yes`, `No`和`PrettyMutch`三个分支, 被称之为"变体构造器"(或者叫做"变体标签"), 通过竖线`|`分割每个构造器.

**Note**: a variant's constructors need to be capitalized.

**注意**: 一个变体的构造器的首字母必须大写.

## Variant Needs an Explicit Definition | 变体需要显式定义

If the variant you're using is in a different file, bring it into scope like you'd do [for a record](record.md#record-needs-an-explicit-definition):

如果你使用的变体类型在不同的文件中, 需要将其引入到你当前的作用域, 就像你当时在使用[记录类型]做的一样(record.md#record-needs-an-explicit-definition):

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
// Zoo.res
type animal = Dog | Cat | Bird
```
```js
// Empty output
```

</CodeTab>

<CodeTab labels={["ReScript", "JS Output"]}>

```res
// Example.res
let pet: Zoo.animal = Dog // preferred
// or
let pet2 = Zoo.Dog
```
```js
var pet = /* Dog */0;
var pet2 = /* Dog */0;
```

</CodeTab>

## Constructor Arguments | 构造器参数

A variant's constructors can hold extra data separated by comma.

变体构造器可以接受额外的的参数, 这些参数的类型通过逗号分割.

<CodeTab labels={["ReScript", "JS Output"]}>

```res prelude
type account =
  | None
  | Instagram(string)
  | Facebook(string, int)
```
```js
// Empty output
```

</CodeTab>

Here, `Instagram` holds a `string`, and `Facebook` holds a `string` and an `int`. Usage:

这里`Instagram`变体包含一个`string`类型, `Facebook`变体包含`string`和`int`. 比如说: 

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let myAccount = Facebook("Josh", 26)
let friendAccount = Instagram("Jenny")
```
```js
var myAccount = {
  TAG: /* Facebook */1,
  _0: "Josh",
  _1: 26
};
var friendAccount = {
  TAG: /* Instagram */0,
  _0: "Jenny"
};
```

</CodeTab>

### Labeled Variant Payloads (Inline Record) | 标记的变体成员 (内联记录)

If a variant payload has multiple fields, you can use a record-like syntax to label them for better readability:

一个变体的成员包含多个字段, 你可以使用一个类型记录的语法标记它们以提升可读性:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type user =
  | Number(int)
  | Id({name: string, password: string})

let me = Id({name: "Joe", password: "123"})
```
```js
var me = {
  TAG: /* Id */1,
  name: "Joe",
  password: "123"
};
```

</CodeTab>

This is technically called an "inline record", and only allowed within a variant constructor. You cannot inline a record type declaration anywhere else in ReScript.

这在技术上被称为 "内联记录", 并且只允许在变体构造函数中使用, 你不能在ReScript的其他地方内联一个记录类型声明.

Of course, you can just put a regular record type in a variant too:

当然, 你也可以把一个记录类型作为变体的参数.

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type u = {name: string, password: string}
type user =
  | Number(int)
  | Id(u)

let me = Id({name: "Joe", password: "123"})
```
```js
var me = {
  TAG: /* Id */1,
  _0: {
    name: "Joe",
    password: "123"
  }
};
```

</CodeTab>

The output is slightly uglier and less performant than the former.

输出的JS代码比之前要稍微"难看一些", 并且性能也不如之前的.

### Pattern Matching On Variant

See the [Pattern Matching/Destructuring](pattern-matching-destructuring) section later.

请看后面提到的[模式匹配/解构](pattern-matching-destructuring)章节.

## JavaScript Output

A variant value compiles to 3 possible JavaScript outputs depending on its type declaration:

变体值可能会编译为三类不同的输出, 取决于其类型声明:

- If the variant value is a constructor with no payload, it compiles to a number.
- If it's a constructor with a payload, it compiles to an object with the field `TAG` and the field `_0` for the first payload, `_1` for the second payload, etc.
- An exception to the above is a variant whose type declaration contains only a single constructor with payload. In that case, the constructor compiles to an object without the `TAG` field.
- Labeled variant payloads (the inline record trick earlier) compile to an object with the label names instead of `_0`, `_1`, etc. The object might or might not have the `TAG` field as per previous rule.

- 一个变体值是一个没有任何装载的构造器, 那么其会被编译成一个数字.
- 如果一个构造器装载值, 其会被编译成一个有`TAG`字段的`TAG`, 字段`_0`保存第一个装载值, 字段`_1`保存第二个装载值, 诸如此类.
- 上述情况的一个例外是一个类型声明只包含一个带有效装载值的构造函数变体, 在这种情况下, 构造函数会编译成一个没有`TAG`字段的对象.
- 有标签的变体有效载荷(前面的内联记录的技巧)会编译成一个有标签名称的对象, 而不是`_0`, `_1`, 等等. 和之前的规则一样, 该对象可能有也可能没有`TAG`字段.

Check the output in these examples:

检查这些例子的JS编译结果:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type greeting = Hello | Goodbye
let g1 = Hello
let g2 = Goodbye

type outcome = Good | Error(string)
let o1 = Good
let o2 = Error("oops!")

type family = Child | Mom(int, string) | Dad (int)
let f1 = Child
let f2 = Mom(30, "Jane")
let f3 = Dad(32)

type person = Teacher | Student({gpa: float})
let p1 = Teacher
let p2 = Student({gpa: 99.5})

type s = {score: float}
type adventurer = Warrior(s) | Wizard(string)
let a1 = Warrior({score: 10.5})
let a2 = Wizard("Joe")
```
```js
var g1 = /* Hello */0;
var g2 = /* Goodbye */1;

var o1 = /* Good */0;
var o2 = /* Error */{
  _0: "oops!"
};

var f1 = /* Child */0;
var f2 = {
  TAG: /* Mom */0,
  _0: 30,
  _1: "Jane"
};
var f3 = {
  TAG: /* Dad */1,
  _0: 32
};

var p1 = /* Teacher */0;
var p2 = /* Student */{
  gpa: 99.5
};

var a1 = {
  TAG: /* Warrior */0,
  _0: {
    score: 10.5
  }
};
var a2 = {
  TAG: /* Wizard */1,
  _0: "Joe"
};
```

</CodeTab>

## Tips & Tricks | 技巧 & 诀窍

**Be careful** not to confuse a constructor carrying 2 arguments with a constructor carrying a single tuple argument:

**请小心** 不要搞混了两个参数的构造器和以单个元组作为参数的构造器:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type account =
  | Facebook(string, int) // 2 arguments
type account2 =
  | Instagram((string, int)) // 1 argument - happens to be a 2-tuple
```
```js
// Empty output
```

</CodeTab>

### Variants Must Have Constructors | 变体必须要有构造器

If you come from an untyped language, you might be tempted to try `type myType = int | string`. This isn't possible in ReScript; you'd have to give each branch a constructor: `type myType = Int(int) | String(string)`. The former looks nice, but causes lots of trouble down the line.

如果你经常使用无类型语言, 你可能会尝试写出这样的定义`type myType = int | string`. 这在ReScript中是不可能的; 你不得不为每一个分支加上一个构造器: `type myType = Int(int) | String(string)`. 前一个初看起来不错, 但后患无穷.

### Interop with JavaScript | 与JavaScript互操作
_This section assumes knowledge about our JavaScript interop. Skip this if you haven't felt the itch to use variants for wrapping JS functions yet_.

_这一章假设你知道了我们的JavaScript互操作机制. 跳过这一章如果如果你还没有感觉到使用变体来包装JS函数的痛点, 请跳过这部分_

Quite a few JS libraries use functions that can accept many types of arguments. In these cases, it's very tempting to model them as variants. For example, suppose there's a `myLibrary.draw` JS function that takes in either a `number` or a `string`. You might be tempted to bind it like so:

相当多的JS库使用可以接受多种类型参数的函数, 在这种情况下, 将它们建模为变体是非常诱人的. 例如, 假设有一个`myLibrary.draw`JS函数, 可以接受`数字`或`字符串`, 你可能会尝试这样来绑定它:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
// reserved for internal usage
@module("myLibrary") external draw : 'a => unit = "draw"

type animal =
  | MyFloat(float)
  | MyString(string)

let betterDraw = (animal) =>
  switch animal {
  | MyFloat(f) => draw(f)
  | MyString(s) => draw(s)
  }

betterDraw(MyFloat(1.5))
```
```js
var MyLibrary = require("myLibrary");

function betterDraw(animal) {
  MyLibrary.draw(animal._0);
}

betterDraw({
  TAG: /* MyFloat */0,
  _0: 1.5
});
```

</CodeTab>

**Try not to do that**, as this generates extra noisy output. Alternatively, define two `external`s that both compile to the same JS call:

**尽量不要这样做**, 因为这样会产生额外的噪音输出. 或者定义两个`external`都编译为相同的JS调用.

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
@module("myLibrary") external drawFloat: float => unit = "draw"
@module("myLibrary") external drawString: string => unit = "draw"
```
```js
// Empty output
```

</CodeTab>

ReScript also provides [a few other ways](bind-to-js-function.md#modeling-polymorphic-function) to do this.

ReScript也提供了[一些其他方式](bind-to-js-function.md#modeling-polymorphic-function)做这件事.

### Variant Types Are Found By Field Name | 变体类型是通过字段名找到的

Please refer to this [record section](record#tips--tricks). Variants are the same: a function can't accept an arbitrary constructor shared by two different variants. Again, such feature exists; it's called a polymorphic variant. We'll talk about this in the future =).

请看[记录类型的这一节](record#tips--tricks). 变体也是一样的: 一个函数不能随意的接受不同变体的构造器. 再一次声明, 这样的特性是存在的, 我们会在未来继续提到这个特性.

## Design Decisions | 设计决策

Variants, in their many forms (polymorphic variant, open variant, GADT, etc.), are likely _the_ feature of a type system such as ReScript's. The aforementioned `option` variant, for example, obliterates the need for nullable types, a major source of bugs in other languages. Philosophically speaking, a problem is composed of many possible branches/conditions. Mishandling these conditions is the majority of what we call bugs. **A type system doesn't magically eliminate bugs; it points out the unhandled conditions and asks you to cover them**\*. The ability to model "this or that" correctly is crucial.

变体经常以很多种形式出现(多态变体, 展开变体, 通用代数数据类型等), 可能是像ReScript这样的类型系统的最关键的特征. 大哥比方, 先前提到的`option`变体, 完全消除了nullable类型需要, 这是很多其他语言bug的来源. 从哲学的角度来讲, 一个问题是由许多可能的分支/条件组成的. 大部分情况下, 对这些条件的处理的缺失就是我们所说的bug. **类型系统并不能神奇地消除bug; 它指出了未处理的条件并要求你覆盖它们**. 正确建模"这个或那个"的能力是至关重要的.

For example, some folks wonder how the type system can safely eliminate badly formatted JSON data from propagating into their program. They don't, not by themselves! But if the parser returns the `option` type `None | Some(actualData)`, then you'd have to handle the `None` case explicitly in later call sites. That's all there is.

比如说, 有些人想知道类型系统如何安全地消除格式不好的JSON数据传播到他们的程序中. 类型系统无法通过自己做到. 但是如果一个parser返回了`option`类型, `None | Some(actualData)`, 那么你就不得不在后续的调用点显式的处理`None`分支. 就这样.

Performance-wise, a variant can potentially tremendously speed up your program's logic. Here's a piece of JavaScript:

从性能上讲, 变体可以潜在地极大地加快你的程序逻辑. 这里有一段JavaScript的内容:

```js
let data = 'dog'
if (data === 'dog') {
  ...
} else if (data === 'cat') {
  ...
} else if (data === 'bird') {
  ...
}
```

There's a linear amount of branch checking here (`O(n)`). Compare this to using a ReScript variant:

这里有一个线性复杂度的分支检查(`O(n)`). 与使用ReScript变体的比较:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type animal = Dog | Cat | Bird
let data = Dog
switch data {
| Dog => Js.log("Wof")
| Cat => Js.log("Meow")
| Bird => Js.log("Kashiiin")
}
```
```js
console.log("Wof");

var data = /* Dog */0;
```

</CodeTab>

The compiler sees the variant, then

1. conceptually turns them into `type animal = 0 | 1 | 2`
2. compiles `switch` to a constant-time jump table (`O(1)`).


如果编译器看到了变体, 然后:
1. 从概念上把它们转变为 `type animal = 0 | 1 | 2`
2. 把`switch`编译成一个拥有常量时间的跳转表(`O(1)`).

---
title: "Null, Undefined 和 Option"
description: "JS interop with nullable and optional values in ReScript"
canonical: "/docs/manual/latest/null-undefined-option"
---

# Null, Undefined 和 Option

ReScript itself doesn't have the notion of `null` or `undefined`. This is a _great_ thing, as it wipes out an entire category of bugs. No more `undefined is not a function`, and `cannot access someAttribute of undefined`!

ReScript语言是没有`null`和`undefined`的. 这是件 _好事_, 因为这抹除了一整类bug. 不会再有 `undefined is not a function` 和 `cannot access someAttribute of undefined` 这种错误了!

However, the **concept** of a potentially nonexistent value is still useful, and safely exists in our language.

然而, 可能不存在的**概念**仍然是有用的, 并且在ReScript中是安全的存在的.

We represent the existence and nonexistence of a value by wrapping it with the `option` type. Here's its definition from the standard library:

我们通过在类型外包裹一层`option`类型来表示可能存在或不存在的值. 这是标准库中的定义:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type option<'a> = None | Some('a)
```
```js
// Empty output
```

</CodeTab>

It means "a value of type option is either None (representing nothing) or that actual value wrapped in a Some".

这意味着: "option类型的值可以是None(表示什么也没有)或者被Some包裹的一个真实的值". 

**Note** how the `option` type is just a regular [variant](variant.md).

**注意** `option`类型只是一个常规的[变体](variant.md).

## 例子

Here's a normal value:

这是一个普通的值:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let licenseNumber = 5
```
```js
var licenseNumber = 5;
```

</CodeTab>

To represent the concept of "maybe null", you'd turn this into an `option` type by wrapping it. For the sake of a more illustrative example, we'll put a condition around it:

为了表示"可能为空"的概念, 你需要在它外面包裹一层`option`把它变成`option`类型. 为了更有效的展示使用方式, 我们写一个关于这点的条件表达式:

<CodeTab labels={["ReScript", "JS Output"]}>

```res
let licenseNumber =
  if personHasACar {
    Some(5)
  } else {
    None
  }
```
```js
var licenseNumber = personHasACar ? 5 : undefined;
```

</CodeTab>

Later on, when another piece of code receives such value, it'd be forced to handle both cases through [pattern matching](pattern-matching-destructuring.md):

稍后, 我们会看到另一些使用这样的值的代码, 它会通过[模式匹配](pattern-matching-destructuring.md)强制处理两种分支情况.

<CodeTab labels={["ReScript", "JS Output"]}>

```res
switch licenseNumber {
| None =>
  Js.log("The person doesn't have a car")
| Some(number) =>
  Js.log("The person's license number is " ++ Js.Int.toString(number))
}
```
```js
var number = licenseNumber;

if (number !== undefined) {
  console.log("The person's license number is " + number.toString());
} else {
  console.log("The person doesn't have a car");
}
```

</CodeTab>

By turning your ordinary number into an `option` type, and by forcing you to handle the `None` case, the language effectively removed the possibility for you to mishandle, or forget to handle, a conceptual `null` value! **A pure ReScript program doesn't have null errors**.

通过把常规的数字类型转换为`option`类型, 并且强制你处理`None`分支, ReScript有效的移除了所有 误处理 或者 忘记处理 一个概念上为`null`的值的可能性! **纯ReScript程序不会有null错误**.

## Interoperate with JavaScript `undefined` and `null` | 与JavaScript的`undefined`和`null`互操作

The `option` type is common enough that we special-case it when compiling to JavaScript:

`option`类型很常见, 所以我们在编译成JavaScript时会对其进行特殊处理:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let x = Some(5)
```
```js
var x = 5;
```

</CodeTab>

simply compiles down to `5`, and

会简洁的编译为`5`, 并且

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let x = None
```
```js
var x;
```

</CodeTab>

compiles to `undefined`! If you've got e.g. a string in JavaScript that you know might be `undefined`, type it as `option<string>` and you're done! Likewise, you can send a `Some(5)` or `None` to the JS side and expect it to be interpreted correctly =)

会编译为`undefined`! 如果你在JavaScript中有一个字符串, 你知道它可能是 "未定义的", 把它作为 `option<string>`输入就可以了. 同样, 你可以向JS端发送一个`Some(5)`或`None`, 它也能被正确解释 =)

### Caveat 1 | 警告 1

The option-to-undefined translation isn't perfect, because on our side, `option` values can be composed:

`option`到`undefined`的编译并不完美, 因为在ReScript中, `option`值是可以组合的:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let x = Some(Some(Some(5)))
```
```js
var x = 5;
```

</CodeTab>

This still compiles to `5`, but this gets troublesome:

这仍然会编译为`5`, 但是带来了一些困扰: 

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let x = Some(None)
```
```js
var Caml_option = require("./stdlib/caml_option.js");

var x = Caml_option.some(undefined);
```

(See output tab).

</CodeTab>

What's this `Caml_option.some` thing? Why can't this compile to `undefined`? Long story short, when dealing with a polymorphic `option` type (aka `option<'a>`, for any `'a`), many operations become tricky if we don't mark the value with some special annotation. If this doesn't make sense, don't worry; just remember the following rule:

这里的`Caml_option.some`是什么? 为什么不饿能直接编译成`undefined`? 长话短说, 当处理多态的`option`类型时(也就是 `option<'a>`, `'a`为任意的类型), 很多操作如果我们不为值加上特殊的签名都会变得棘手. 如果这不够有说服力, 别担心; 记住下面的规则:

- **Never, EVER, pass a nested `option` value (e.g. `Some(Some(Some(5)))`) into the JS side.**
- **Never, EVER, annotate a value coming from JS as `option<'a>`. Always give the concrete, non-polymorphic type.**

- **永远不要传递一个嵌套的`option`值(比如说: `Some(Some(Some(5)))`)到JS**
- **永远不要标注一个来自于JS的值为`option<'a>`类型. 每次都把具体的非多态的类型加上.**

### Caveat 2 | 警告 2

Unfortunately, lots of times, your JavaScript value might be _both_ `null` or `undefined`. In that case, you unfortunately can't type such value as e.g. `option<int>`, since our `option` type only checks for `undefined` and not `null` when dealing with a `None`.

不幸的是, 大部分的时间, 你的JavaScript值可能 _同时_ 是`null`或`undefined`. 很不幸, 在那种情况下, 你不能把那个值当作`option<int>`类型, 因为当处理`None`的时候, 我们的`option`类型只会检查`undefined`而不是`null`.

#### Solution: More Sophisticated `undefined` & `null` Interop | 解决方法: 更加复杂的`undefined`和`null`互操作

To solve this, we provide access to more elaborate `null` and `undefined` helpers through the [`Js.Nullable`](api/js/nullable) module. This somewhat works like an `option` type, but is different from it.

为了解决这个问题, 我们通过[`Js.Nullable`](api/js/nullable)模块提供对更复杂的`null`和`undefined`的帮助函数. 这有点像`option`类型, 但又与之不同. 


#### Examples | 例子

To create a JS `null`, use the value `Js.Nullable.null`. To create a JS `undefined`, use `Js.Nullable.undefined` (you can naturally use `None` too, but that's not the point here; the `Js.Nullable.*` helpers wouldn't work with it).

要创建一个JS的`null`, 可以使用`Js.Nullable.null`. 要创建一个JS`undefined`, 可以使用`Js.Nullable.undefined` (你自然也可以使用`None`, 但这不是这里的重点; `Js.Nullable.*`里的帮助函数不会对它起作用).

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
@module("MyConstant") external myId: Js.Nullable.t<string> = "myId"
```
```js
// Empty output
```

</CodeTab>

To create such a nullable string from our side (presumably to pass it to the JS side, for interop purpose), do:

如果要从RsScript这边创建这样一个可空(nullable)的字符串(先假定是为了把它传递给JS那边, 以达到互操作的目的)可以这么做:


<CodeTab labels={["ReScript", "JS Output"]}>

```res example
@module("MyIdValidator") external validate: Js.Nullable.t<string> => bool = "validate"
let personId: Js.Nullable.t<string> = Js.Nullable.return("abc123")

let result = validate(personId)
```
```js
var MyIdValidator = require("MyIdValidator");
var personId = "abc123";
var result = MyIdValidator.validate(personId);
```

</CodeTab>

The `return` part "wraps" a string into a nullable string, to make the type system understand and track the fact that, as you pass this value around, it's not just a string, but a string that can be `null` or `undefined`.

`return`的那部分是将一个字符串"包裹"成一个可空的字符串, 以使类型系统理解并跟踪这样一个事实, 即当你传递这个值时, 它不仅仅是一个字符串, 而是一个可以是`null`或`undefined`的字符串.

#### Convert to/from `option` | 与`option`值互相转换

`Js.Nullable.fromOption` converts from a `option` to `Js.Nullable.t`. `Js.Nullable.toOption` does the opposite.
`Js.Nullable.fromOption`将一个`option`转换到`Js.Nullable.t`. `Js.Nullable.toOption`做反向的转换.

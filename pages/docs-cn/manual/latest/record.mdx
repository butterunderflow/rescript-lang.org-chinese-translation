---
title: "记录"
description: "Record types in ReScript"
canonical: "/docs/manual/latest/record"
---

# Record | 记录

Records are like JavaScript objects but:

记录和JavaScript中的object类似, 但有如下特点:

- are immutable by default
- have fixed fields (not extensible)

- 默认是不可变的
- 成员字段是固定的(无法扩展)

## Type Declaration | 类型声明

A record needs a mandatory type declaration:

一个记录需要一个显式的类型声明: 

<CodeTab labels={["ReScript", "JS Output"]}>

```res prelude
type person = {
  age: int,
  name: string,
}
```
```js
// Empty output
```

</CodeTab>

## Creation

To create a `person` record (declared above):

创建一个类型为`person`记录(之前已经声明过了):

<CodeTab labels={["ReScript", "JS Output"]}>

```res prelude
let me = {
  age: 5,
  name: "Big ReScript"
}
```
```js
var me = {
  age: 5,
  name: "Big ReScript"
};
```

</CodeTab>

When you create a new record value, ReScript tries to find a record type declaration that conforms to the shape of the value. So the `me` value here is inferred as of type `person`.

当你创建记录值的时候, ReScript会尝试寻找和记录值相匹配的记录类型. 所以这里的`me`会被推导为类型`person`.

The type is found by looking above the `me` value. **Note**: if the type instead resides in another file or module, you need to explicitly indicate which file or module it is:

类型可以通过观察`me`的值推导而来. 如果类型的定义在另一个文件或者模块, 那么你必须要指明在哪个模块:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
// School.res
type person = {age: int, name: string}
```
```js
// Empty output
```

</CodeTab>

<CodeTab labels={["ReScript", "JS Output"]}>

```res
// Example.res

let me: School.person = {age: 20, name: "Big ReScript"}
/* or */
let me2 = {School.age: 20, name: "Big ReScript"}
```
```js
var me = {
  age: 20,
  name: "Big ReScript"
};
var me2 = {
  age: 20,
  name: "Big ReScript"
};
```

</CodeTab>

Either of the above 3 says "this record's definition is found in the School file". The first one, the regular type annotation, is preferred.

上面3个定义在告诉我们"所定义的记录的类型定义在School文件中". 第一个, 即常规的类型注释是首选.


## Access | 记录访问

Use the familiar dot notation:

使用大家熟悉的点(`.`)表示访问记录

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let name = me.name
```
```js
var name = "Big ReScript";
```

</CodeTab>

## Immutable Update

New records can be created from old records with the `...` spread operator. The original record isn't mutated.

新的记录可以通过旧的记录加上展开运算符`...`创建. 原始的记录是不会被改变的.

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
let meNextYear = {...me, age: me.age + 1}
```
```js
var meNextYear = {
  age: 21,
  name: "Big ReScript"
};
```

</CodeTab>

**Note**: spread cannot add new fields to the record value, as a record's shape is fixed by its type.

**注意**: 展开运算符不会给记录添加新的字段, 因为记录的"形状"已经根据被类型固定了.

## Mutable Update | 可变更新

Record fields can optionally be mutable. This allows you to efficiently update those fields in-place with the `=` operator.

记录的字段可以被设置为可变的, 这让你可以高效的通过`=`运算符原地更新这些字段.

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type person = {
  name: string,
  mutable age: int
}

let baby = {name: "Baby ReScript", age: 5}
baby.age = baby.age + 1 // `baby.age` is now 6. Happy birthday!
```
```js
var baby = {
  name: "Baby ReScript",
  age: 5
};

baby.age = baby.age + 1 | 0;
```

</CodeTab>

Fields not marked with `mutable` in the type declaration cannot be mutated.

没有在类型声明中被标记为`mutable`的字段是不能被修改的.

## JavaScript Output | JavaScript输出

ReScript records compile to straightforward JavaScript objects; see the various JS output tabs above.

ReScript的记录会直接编译为JavaScript的对象; 可以查看以上表格的JS输出.

## Optional Record Fields | 可选记录字段
ReScript [`v10`](/blog/release-10-0-0#experimental-optional-record-fields) introduced optional record fields. This means that you can define fields that can be omitted when creating the record. It looks like this:

ReScript [`v10`](/blog/release-10-0-0#experimental-optional-record-fields)引入了可选记录字段. 这意味着你可以在创建记录的时候忽略一些字段. 比如说像这样: 

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type person = {
  age: int,
  name?: string
}
```
```js
// Empty output
```

</CodeTab>

Notice how `name` has a suffixed `?`. That means that the field itself is _optional_.

 注意在`name`有个`?`后缀. 这意味着字段本身的是 _可选的_.

### Creation | 创建可选字段
You can omit any optional fields when creating a record. Not setting an optional field will default the field's value to `None`:

你可以在创建记录的时候忽略任意的可选字段, 未被设置的字段会被赋予一个默认值`None`.

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type person = {
  age: int,
  name?: string
}

let me = {
  age: 5,
  name: "Big ReScript"
}

let friend = {
  age: 7
}
```
```js
var me = {
  age: 5,
  name: "Big ReScript"
};

var friend = {
  age: 7
};
```

</CodeTab>

This has consequences for pattern matching, which we'll expand a bit on soon.

这会对模式匹配造成一定的负担, 我们稍后再展开讲这一点.

## Immutable Update | 不可变更新
Updating an optional field via an immutable update above lets you set that field value without needing to care whether it's optional or not.

通过不可变更新来更新一个可选字段的时候, 你不需要其值是否真的是一个可选值.

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type person = {
  age: int,
  name?: string
}

let me = {
  age: 123,
  name: "Hello"
}

let withoutName = {
  ...me,
  name: "New Name"
}
```
```js
import * as Caml_obj from "./stdlib/caml_obj.js";

var me = {
  age: 123,
  name: "Hello"
};

var newrecord = Caml_obj.obj_dup(me);

newrecord.name = "New Name";

var withoutName = newrecord;
```

</CodeTab>


However, if you want to set the field to an optional value, you prefix that value with `?`:

如果你需要把一个可选字段更新为可选值, 你需要在值之前加上 `?`:

<CodeTab labels={["ReScript", "JS Output"]}>

```res example
type person = {
  age: int,
  name?: string
}

let me = {
  age: 123,
  name: "Hello"
}

let maybeName = Some("My Name")

let withoutName = {
  ...me,
  name: ?maybeName
}
```
```js
import * as Caml_obj from "./stdlib/caml_obj.js";

var me = {
  age: 123,
  name: "Hello"
};

var maybeName = "My Name";

var newrecord = Caml_obj.obj_dup(me);

newrecord.name = maybeName;

var withoutName = newrecord;
```

</CodeTab>

You can unset an optional field's value via that same mechanism by setting it to `?None`.

你可以通过同样的机制将一个可选字段的值设置为`?None'来取消它.

### Pattern Matching on Optional Fields | 可选字段的模式匹配
[Pattern matching](pattern-matching-destructuring), one of ReScript's most important features, has two caveats when you deal with optional fields.

[模式匹配](pattern-matching-destructuring), 是ReScript最重要的特性之一, 但当你处理可选字段时, 有两点需要注意.

When matching on the value directly, it's an `option`. Example:

当直接在值上进行匹配的时候, 其是一个option值. 例如:

<CodeTab labels={["ReScript", "JS Output"]}>

```res
type person = {
  age: int,
  name?: string,
}

let me = {
  age: 123,
  name: "Hello",
}

let isRescript = switch me.name {
| Some("ReScript") => true
| Some(_) | None => false
}
```
```js
var isRescript;

isRescript = "Hello" === "ReScript" ? true : false;

var me = {
  age: 123,
  name: "Hello"
};
```

</CodeTab>

But, when matching on the field as part of the general record structure, it's treated as the underlying, non-optional value:

但是, 当把这个字段作为通用的记录结构的一部分进行匹配时, 它被视为基本的且非选择的值:

<CodeTab labels={["ReScript", "JS Output"]}>

```res
type person = {
  age: int,
  name?: string,
}

let me = {
  age: 123,
  name: "Hello",
}

let isRescript = switch me {
| {name: "ReScript"} => true
| _ => false
}

```
```js
var isRescript;

isRescript = "Hello" === "ReScript" ? true : false;

var me = {
  age: 123,
  name: "Hello"
};
```

</CodeTab>

Sometimes you _do_ want to know whether the field was set or not. You can tell the pattern matching engine about that by prefixing your option match with `?`, like this:

有的时候你真的想要确认字段的值是否被设置. 你可以在你的可选值的匹配前加上`?`, 以此告知模式匹配机, 像这样:

<CodeTab labels={["ReScript", "JS Output"]}>

```res
type person = {
  age: int,
  name?: string,
}

let me = {
  age: 123,
  name: "Hello",
}

let nameWasSet = switch me {
| {name: ?None} => false
| {name: ?Some(_)} => true
}
```
```js
var nameWasSet = false;

var me = {
  age: 123,
  name: "Hello"
};
```

</CodeTab>

## Tips & Tricks | 技巧和诀窍

### Record Types Are Found By Field Name | 记录类型可以通过字段的名字找到
With records, you **cannot** say "I'd like this function to take any record type, as long as they have the field `age`". The following **won't work as intended**:

对于记录, 你**不能**说 "我希望这个函数可以接受任何记录类型, 只要它们有`年龄'这个字段". 下面的**不会按预期工作**. 

<CodeTab labels={["ReScript", "JS Output"]}>

```res
type person = {age: int, name: string}
type monster = {age: int, hasTentacles: bool}

let getAge = (entity) => entity.age
```
```js
function getAge(entity) {
  return entity.age;
}
```

</CodeTab>

Instead, `getAge` will infer that the parameter `entity` must be of type `monster`, the closest record type with the field `age`. The following code's last line fails:

相反, `getAge`将推断参数`entity`必须是`monster`类型, 这是与字段`age`最接近的记录类型. 下面代码的最后一行会报错:


```res
let kraken = {age: 9999, hasTentacles: true}
let me = {age: 5, name: "Baby ReScript"}

getAge(kraken)
getAge(me) // type error!
```

The type system will complain that `me` is a `person`, and that `getAge` only works on `monster`. If you need such capability, use ReScript objects, described [here](object.md).

类型系统会抱怨`me`是一个`person`类型, 而且`getAge`只对`monster`有效. 如果你需要这样的能力, 请使用ReScript中的[对象](object.md)描述.

### Optional Fields in Records Can Be Useful for Bindings | ReScript中的可选字段在绑定中很有用
Many JavaScript APIs tend to have large configuration objects that can be a bit annoying to model as records, since you previously always needed to specify all record fields when creating a record. 

很多JavaScript API往往有庞大的对象用于配置, 这些对象如果用记录的来模拟可能有点令人讨厌, 因为你以前总是需要在创建记录时指定所有记录字段.

Optional record fields, introduced in [`v10`](/blog/release-10-0-0#experimental-optional-record-fields), is intended to help with this. Optional fields will let you avoid having to specify all fields, and let you just specify the one's you care about. A significant improvement in ergonomics for bindings and other APIs with for example large configuration objects.

在[`v10`](/blog/release-10-0-0#experimental-optional-record-fields)中引入的多可选记录字段就是为了帮助解决这个问题. 多可选字段将使你不必指定所有字段, 而只需指定你所关心的字段. 对于绑定和其他具有大型配置对象的API来说, 这是对"人体工程学"的重大改进.

## Design Decisions | 设计决策

After reading the constraints in the previous sections, and if you're coming from a dynamic language background, you might be wondering why one would bother with record in the first place instead of straight using object, since the former needs explicit typing and doesn't allow different records with the same field name to be passed to the same function, etc.

在阅读了前面几节的约束条件后, 如果你是动态语言背景的人你可能会想, 为什么首先要用记录而不是直接用object? 因为记录需要明确的类型化, 而且不允许将具有相同字段名的不同记录传递给同一个函数, 等等. 

1. The truth is that most of the times in your app, your data's shape is actually fixed, and if it's not, it can potentially be better represented as a combination of variant (introduced next) + record instead.
2. Since a record type is resolved through finding that single explicit type declaration (we call this "nominal typing"), the type error messages end up better than the counterpart ("structural typing", like for tuples). This makes refactoring easier; changing a record type's fields naturally allows the compiler to know that it's still the same record, just misused in some places. Otherwise, under structural typing, it might get hard to tell whether the definition site or the usage site is wrong.

1. 实际情况是, 在你的应用程序中, 大多数时候你的数据的形状实际上是固定的, 如果不是, 它有可能更好地表示为变体(接下来将介绍)+记录的组合. 
2. 由于记录类型是通过找到那个单一的显式类型声明来解决的(我们称之为 "名义类型(nominal typing)"), 类型错误信息最终会比对应的类型("结构化类型(structural typing)", 如元组)更好. 这使得重构更容易; 改变一个记录类型的字段自然允许编译器确认它仍然和之前是同一个记录, 只是在某些地方被误用. 否则, 在结构化类型下, 可能会变得很难区分是定义的地方还是使用的地方出了问题.
